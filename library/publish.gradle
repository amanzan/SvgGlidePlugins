apply plugin: 'digital.wup.android-maven-publish'
apply plugin: 'com.jfrog.bintray'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

/*
 * Copyright 2018 Kirill Rozov
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "kirich1409"
            name "Kirill Rozov"
            email "kirich1409@gmail.com"
        }
    }

    scm {
        url "https://github.com/kirich1409/SvgGlidePlugins.git"
    }
}

publishing {
    publications {
        mavenAar(MavenPublication) {
            from components.android
            groupId 'com.kirich1409.svgplugin'
            artifactId 'glide4'
            version rootProject.ext.versionName
            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'SvgGlide4Plugin')
                root.appendNode('url', 'https://github.com/kirich1409/SvgGlidePlugins')
                root.children().last() + pomConfig
            }

            artifact sourceJar
        }
    }
}

bintray {
    user = System.env.BINTRAY_USER ?: ""
    key = System.env.BINTRAY_API_KEY ?: ""
    publications = ['mavenAar']

    pkg {
        repo = 'maven'
        name = 'svg-glide-plugin'
        desc = "Glide 4 Module which adds possibility to load SVG"
        userOrg = System.env.BINTRAY_USER_ORGANIZATION ?: ""
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/kirich1409/SvgGlidePlugins.git'
        websiteUrl = 'https://github.com/kirich1409/SvgGlidePlugins'
        issueTrackerUrl = 'https://github.com/kirich1409/SvgGlidePlugins/issues'
        labels = ['glide', 'svg']

        githubRepo = 'kirich1409/SvgGlidePlugins' //Optional Github repository
        githubReleaseNotesFile = 'README.md' //Optional Github readme file

        version {
            name = rootProject.ext.versionName
            vcsTag = rootProject.ext.versionName
        }
    }
}
